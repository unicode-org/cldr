<?xml version="1.0" encoding="UTF-8" ?>
<!DOCTYPE supplementalData SYSTEM "../../common/dtd/ldmlSupplemental.dtd">
<!-- Copyright © 1991-2015 Unicode, Inc.
CLDR data files are interpreted according to the LDML specification (http://unicode.org/reports/tr35/)
For terms of use, see http://www.unicode.org/copyright.html -->
<supplementalData>
	<version number="$Revision$" />
	<transforms>
		<transform source="ia" target="ia_FONIPA" direction="forward" draft="contributed" alias="ia-fonipa-t-ia">
			<tRule>
# Transformation from Interlingua (ia) to its IPA transcription (ia_FONIPA).
# http://en.wikipedia.org/wiki/Interlingua#Interlingua_alphabet
# http://www.omniglot.com/writing/interlingua.htm

::NFC;
::Lower;

# Interlinua has five falling diphthongs.
# http://en.wikipedia.org/wiki/Interlingua#Orthography_and_pronunciation
ai → ai̯;
au → au̯;
ei → ei̯;  # rare
eu → eu̯;
oi → oi̯;  # rare

# ‹g› is usually [ɡ], but it is [d͡ʒ] in -age, -agi-, and -egi-.
# http://www.omniglot.com/writing/interlingua.htm
$vowel = [aeiouy];
$end_of_word = [$ ];
{age} $end_of_word → ad͡ʒe;
{agi} $vowel → ad͡ʒ;  # viagiar → viad͡ʒar
agi → ad͡ʒi;
{egi} $vowel → ed͡ʒ;  # legier → led͡ʒer
egi → ed͡ʒi;
gg → ɡ;
g → ɡ;

# Omniglot: “The sounds of g and k assimilate a preceding n as in English.”
{n} [gkqx] → ŋ;
nn → n;
n → n;

a → a;
bb → b;
b → b;
cc → k;
{c} [ei] → t͡s;
ch → k;
c → k;
dd → d;
d → d;
e → e;
ff → f;
f → f;
h → ;  # ‹h› is normally silent.
i → i;
j → ʒ;
kk → k;
k → k;
ll → l;
l → l;
mm → m;
m → m;
o → o;
ph → f;  # philosophos, physica
pp → p;
p → p;
que → ke;
qu → kw;
q → k;
rr → ɾ;
r → ɾ;
sh → ʃ;  # rare
ss → s;
s → s;
[^s] {ti} [aeiouy] → t͡sj;
tt → t;
t → t;
u → u;
v → v;
w → v;
x → ks;
y → i;
z → z;
			</tRule>
		</transform>
	</transforms>
</supplementalData>
